
// get desired utility name
find_by_zipcode = yesno( 'Find utility by zip code?\nChoose Yes to find by zip code. Choose No to choose from a list of all available utilities.');
if ( find_by_zipcode ) 
{
	zip = in( 'Enter the zip code: ', '00000', 'Zip Code' );
	outln( 'Getting list of utilities for ' + zip + '.');
	utility_list = urdb_list_utilities_by_zip_code ( zip );
		if ( utility_list == null )
	{
		outln( 'No utilities found for zip code ' + zip + '. Exiting script.' );
		exit;
	}
}
else 
{
	outln( 'Getting list of all utilities.' );
	utility_list = urdb_list_utilities();
}
utility_choice = choose_from_list( utility_list, 'Choose a utility:','Choose Utility',0);

// get list of rate names and id numbers (guid) available for desired utility
outln( 'Getting list of rates available for ' + utility_choice + '.' );
rate_list = urdb_list_rates( utility_choice );

// separate rate names from rate guids
i=0;
n=0;
g=0;
while ( i<#rate_list )
{
	if ( mod(i,2) == 0 ) 
	{
		rate_name_list[n] = rate_list[i];
		n++;
	}	
	else
	{
		rate_guid_list[g] = rate_list[i];
		g++;
	}		
	i++;
}

// get desired rate name
if ( #rate_list > 0 )
{
	rate_choice = choose_from_list( rate_name_list, 'Choose a rate:', 'Chooose Rate', 0);
}
else
{
	outln( 'No rates available for ' + utility_choice + '. Exiting script.');
	exit;
}

// look up guid for desired rate name	
for ( i=0; i<#rate_name_list; i++ )
{
	if ( rate_name_list[i] == rate_choice ) rate_index = i;
}

// get rate data for desired guid	
outln( 'Downloading rate data for ' + rate_name_list[rate_index] + '.' );
rate_table = urdb_get( rate_guid_list[rate_index] );	

// assign rate table data to SAM user inputs
keys = @rate_table;
for ( i=0; i<#keys; i++ )
{
	set( 'ur_' + keys[i] , rate_table{keys[i]} );
}
outln( 'Rate data assigned to SAM inputs on Electricity Rates page.' );