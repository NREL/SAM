public_versions = ['2014.11.24', '2015.1.30', '2015.6.30', '2016.3.14', '2017.1.17'];


file = homedir() + '/sam_users.csv';

if ( yesno('download latest database file?') )
{
	url = 'https://developer.nrel.gov/api/sam/v1/tracker/user_stats?api_key=rJzFOTOJhNHcLOnPmW2TNCLV8I4HHLgKddAycGpn';
	curl( url, {file=file, message='downloading...'});
}

outln('loading ' + file );
x = csvread( file );

// column indices in CSV file download
enum { user_email,
	user_key,
	user_created_at,
	user_updated_at,
	sam_app_name,
	sam_version,
	usage_count,
	ip_address,
	ip_city,
	ip_region_code,
	ip_region_name,
	ip_country_code,
	ip_country_name,
	ip_latitude,
	ip_longitude };

nusers = 0;
nusage = 0;
outln(date_time());
outln('reading database...');
function approx_days_since( year, month, day )
{
	// Mon Jul 13 12:00:49 2015
	date = split(date_time(), ' ');
	cyear = to_int(date[4]);
	cday = to_int(date[2]);
	smonths = [ 'Jan', 'Feb', 'Mar', 'Apr', 'May', 'Jun', 'Jul', 'Aug', 'Sep', 'Oct', 'Nov', 'Dec' ];
	for(cmonth=0;cmonth<12;cmonth++ )
		if ( smonths[cmonth] == date[1] )
			break;
				
	start = (year-1971)*365 + (month-1)*30 + day;
	end = (cyear-1971)*365 + (cmonth)*30 + cday;
	return end-start;
}

for( i=0;i<#public_versions;i++ )
	nusers{public_versions[i]} = 0;

platusers = {};
platusage = {};
ver_usage = {};
domain_users = {};
domain_usages = {};
total_usage = 0;
N = #x;
for( i=1;i<N;i++ )
{
	sver = split(x[i][ sam_version ], '-');
	sam_ver = sver[0];
	plat = sver[1];
	// email to find domain
	email = split(x[i][user_email],'.');
	domain = "";
	if (#email > 0)
		domain = lower(replace(email[#email-1],'\\',''));
	
	
	if ( !( nusers ?@ sam_ver )) continue;	
	if ( !( ver_usage ?@ sam_ver )) ver_usage{sam_ver} = 0;
	if ( !( platusers ?@ plat )) platusers{plat} = 0;
	if ( !( platusage ?@ plat )) platusage{plat} = 0;
	if ( !( domain_users ?@ domain )) domain_users{domain} = 0;
	if ( !( domain_usages ?@ domain )) domain_usages{domain} = 0;
	
	nusers{sam_ver}++;
	usage = to_int( x[i][ usage_count ]);
	platusers{plat}++;
	platusage{plat} += usage;
	domain_users{domain}++;
	domain_usages{domain} += usage;
	ver_usage{sam_ver} += usage;	
	total_usage += usage;
}

vers = @ver_usage;
for( i=0;i<#vers;i++ )
{
	outln( 'number of users registered for version ' + vers[i] + ': ' + nusers{vers[i]} );
	outln( 'total usage count for version ' + vers[i] + ': ' + ver_usage{vers[i]} );
}

outln( 'platforms (by registrations): ' + platusers );
outln( 'platforms (by usage): ' + platusage );

outln( 'domain (by registrations): ' + domain_users );
outln( 'domain (by usage): ' + domain_usages );


maxper=0;
keys = @platusage;
for(i=0;i<#keys;i++ )
{
	labels[i][0] = i;
	labels[i][1] = keys[i];
	xx[i] = i;
	per = 100*platusage{keys[i]}/total_usage;
	yy[i] = per;
	if ( per > maxper ) maxper = per;
}
outln(xx);
outln(yy);
	
newplot(true);
plot(xx,yy, {'type'='bar', size=20});
axis('x1', {'type'='label', 'labels'=labels,'label'='Platform', 'min'=-1, 'max'=#keys});
axis('y1', {'label'='Percentage (%)', 'max'=maxper*1.05, 'min'=0});
plotopt({'legend'=false, 'title'='Usage by operating system as of ' + date_time()});



maxusers=0;
keys = @domain_users;
for(i=0;i<#keys;i++ )
{
	labels[i][0] = i;
	labels[i][1] = keys[i];
	xx[i] = i;
	users = domain_users{keys[i]};
	yy[i] = users;
	if ( users > maxusers ) maxusers = users;
}
outln(xx);
outln(yy);
	
newplot(false);
plot(xx,yy, {'type'='bar', size=20});
axis('x1', {'type'='label', 'labels'=labels,'label'='Domain', 'min'=-1, 'max'=#keys});
axis('y1', {'label'='Number users', 'max'=maxper, 'min'=0});
plotopt({'legend'=false, 'title'='Users by domain as of ' + date_time()});



maxper=0;
keys = @domain_usages;
for(i=0;i<#keys;i++ )
{
	labels[i][0] = i;
	labels[i][1] = keys[i];
	xx[i] = i;
	per = 100*domain_usages{keys[i]}/total_usage;
	yy[i] = per;
	if ( per > maxper ) maxper = per;
}
outln(xx);
outln(yy);
	
newplot(false);
plot(xx,yy, {'type'='bar', size=20});
axis('x1', {'type'='label', 'labels'=labels,'label'='Domain', 'min'=-1, 'max'=#keys});
axis('y1', {'label'='Percentage (%)', 'max'=maxper*1.05, 'min'=0});
plotopt({'legend'=false, 'title'='Usage by domain as of ' + date_time()});


xx = yy = labels = [];
for( i=0;i<#public_versions;i++ )
{
	labels[i][0] = i;
	labels[i][1] = public_versions[i];
	
	xx[i] = i;
	yy[i] = ver_usage{public_versions[i]};
	yy2[i] = nusers{public_versions[i]};
}


newplot(false);
plot(xx,yy, {'type'='bar', size=20});
axis('x1', {'type'='label', 'labels'=labels,'label'='Version', 'min'=-1, 'max'=#public_versions});
axis('y1', {'label'='Usage count', 'max'=max(yy)*1.05, 'min'=0});
plotopt({'legend'=false, 'title'='Usage by version as of ' + date_time()});


newplot(false);
plot(xx,yy2, {'type'='bar', size=20});
axis('x1', {'type'='label', 'labels'=labels,'label'='Version', 'min'=-1, 'max'=#public_versions});
axis('y1', {'label'='Registrations count', 'max'=max(yy2)*1.05, 'min'=0});
plotopt({'legend'=false, 'title'='Registrations by version as of ' + date_time()});


outln( '\ntotal number of users: ' + N );
outln( 'total usage: ' + total_usage );
outln( 'usage per person: ' + (total_usage/N) );
ndays = approx_days_since( 2014, 11, 24 );
outln( 'days since 2014/11/24: ' + ndays );
outln( 'average usage per day: ' + (total_usage/ndays) );