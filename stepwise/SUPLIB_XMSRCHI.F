      SUBROUTINE XMSRCHI (IVEC, ILBIN, IUBIN, NUM, ICOD, LOC)
C
C     THIS SUBROUTINE SEARCHES ROW ILB THOURGH ROW IUB OF A
C     NUMERICALLY ORDERED INTEGER COLUMN VECTOR FOR THE OCCURANCE
C     THE VALUE NUM.
C     IF NUM IS FOUND ICOD IS SET TO UNITY AND LOC IS THE ROW NUMBER
C     WHERE NUM RESIDES. IF NUM IS NOT FOUND ICOD IS ZERO AND LOC IS
C     THE ROW NUMBER WHERE NUM WOULD RESIDE IF IT WERE IN THE NUMER-
C     ICALLY ORDER LIST.   BOB LUST
C
C  THIS SUBROUTINE HAS BEEN CHANGED FROM BOB LUST'S VERSION
C  AND NOW ASSUMES THAT THERE IS NO MORE THAN ONE MATCH
C  IN THE ORDERED LIST 'IVEC'.  BILL MILLS-CURRAN  JAN. 1, 1983
C
C  IVEC    ORDERED LIST
C
C  ILBIN   LOW NUMBERED ROW OF SEARCH RANGE
C
C  IUBIN   HIGH NUMBERED ROW OF SEARCH RANGE
C
C  NUM     VALUE TO BE LOCATED IN IVEC
C
C  ICOD    RETURN CODE  0 = NO MATCH   1 = MATCH
C
C  LOC     LOCATION IN IVEC FOR NUM
C
      IMPLICIT NONE
C
      INTEGER IVEC(1)  !! note that this may be real data
      INTEGER ILBIN, IUBIN
      INTEGER NUM  !! note that this may be real data
      INTEGER ICOD
      INTEGER LOC
C
      INTEGER ILB, IUB
      INTEGER MID
      INTEGER NVAL
C
      ILB = ILBIN
      IUB = IUBIN
      ICOD = 0
      IF (IUB .LT. ILB) THEN
         LOC = 1
         RETURN
      END IF
C
C  CHECK TO SEE IF NUM IS AT EITHER END OF LIST
C
      IF (IVEC(ILB).GT.NUM) THEN
         LOC = ILB
         RETURN
      ELSE IF (IVEC(IUB).LT.NUM) THEN
         LOC = IUB+1
         RETURN
      END IF
C
C  NUM IS INTERNAL TO IVEC
C
  100 CONTINUE
      MID = (ILB+IUB)/2
      IF (MID.LE.ILB) GOTO 110
      NVAL = IVEC(MID)
      IF (NVAL.LT.NUM) THEN
         ILB = MID
         GOTO 100
      ELSE IF (NVAL.GT.NUM) THEN
         IUB = MID
         GOTO 100
      ELSE
         ICOD = 1
         LOC = MID
         RETURN
      END IF
  110 CONTINUE
      IF (NUM.EQ.IVEC(ILB)) THEN
         ICOD = 1
         LOC = ILB
         RETURN
      ELSE IF (NUM.EQ.IVEC(IUB)) THEN
         ICOD = 1
         LOC = IUB
         RETURN
      ELSE
         LOC = IUB
         RETURN
      END IF
      END
