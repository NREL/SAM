/*************************************/
/**           Runtime Tools         **/

// Save the current dir for later use
curDir = userlocaldatadir();
//outln( curDir );

// Determine screen size and set default pop-up positions

scrn = scrnres();

defaultX = scrn[0] - scrn[0]/2;
defaultY = 60;

// exit function (and removes any highlighting)
function exitIf( exitCondition ){
	if( exitCondition ){
		transp();
		exit;
	}
}

function send_excel( xl, variable)
{
	xl_set( xl, value(variable), variable );
}


// if one is open close
close_project();


/*************************************/
/**         Welcome the User        **/

wS = [500,500]; 
//sdt - added description of SAM currency discrepancies
exitIf( !msgbox("Welcome to the PV for India Wizard!\n" +
	         "This tutorial will provide a series of pop-ups which will guide you through the use of the PV model in India.\n\n" +
			 "Although you will interact with pop-up menus in this wizard, the relevant inputs will be highlighted in SAM so that you can find them later.\n\n"+
				"Users should note that because of potential future work to create a fully internationalized version of SAM, the decision was made not to fully transfer financial inputs into rupees for this iteration of the tool.\n\n" +
				"SAM accepts rupee denominated inputs within the wizard and will provide rupee denominated output values, but this is done through the LK scripting language rather than as a core functionality of SAM. \n\n" +
				"Where necessary, the correct interpretation of input and output currency values has been provided throughout the tool. \n\n", 
			 [defaultX, defaultY, 500, 500]) );
			 





/*************************************/
/**         Case management         **/
/* Will have separate default files to load defaults */
/* For now, load updated file from Sara */
/* copy to user folder and load */
template_file = runtimedir() + "quickstart/PV_for_India.sam";
user_file = curDir + "/PV_for_India.sam";
copy_file(template_file, user_file, true);
exitIf (!open_project(user_file));
	
millisleep(500);


caseName = case_name();
exitIf(caseName == "");


run_wizard_again=true;
while (run_wizard_again)
{

/*************************************/
/**     Get Simulation Location     **/

// Show solar resource page
show_page( 'Location and Resource');

// Add India weather files to solar resource library
indiaWeatherDir = runtimedir() + '/quickstart/pv_india_weather';
x = get_settings('solar_data_paths');
if ( strpos( x, indiaWeatherDir ) < 0 )
{
	if ( x != '' ) x += ';';
	x += indiaWeatherDir;
	set_settings('solar_data_paths', x );
	rescanlibrary( 'solar' );
}

// Highlight resource selection box in UI
focusto('GroupBox2');
geom = widgetpos('GroupBox2');
transp( geom, 'yellow', 70 );
millisleep(500);

// Get Location from User
currentSRD = get('solar_resource');
// parse all solar data files in library to those that start with "India IND"
allSolarDataList = library('SolarResourceData');
count=0;
for (i=0;i<#allSolarDataList;i++)
{
	if (left(allSolarDataList[i], 9) == "India IND")
	{
		solarDataList[count] = allSolarDataList[i];
		count++;
	}
}
//solarDataList = ["India IND Ahmadabad (INTL)","India IND Bombay (INTL)","India IND Calcutta (INTL)","India IND Goa Panaji (INTL)","India IND Madras (INTL)","India IND Nagpur (INTL)","India IND New_Delhi (INTL)","India IND Trivandrum (INTL)"];
initial = 0;
for( i =0; i<#solarDataList; i++){
	if( currentSRD == solarDataList[i] ){
		initial = i;
		break;
	}
}
srd = choose_from_list( 
		solarDataList,
		"Where is your project located?\n\nChoose a location from the selection box\n(or press 'OK' to use the default)",
		"PV resource data",
		initial,
		[defaultX, defaultY, 500, 500]);

exitIf(srd == "");
set('solar_resource', srd);

// Allow user to reflect on their choices
millisleep(1000);
transp( geom, 'yellow', 0 );


/*************************************/
/**     Get System Size    **/

show_page('System Design');

// Highlight resource selection box in UI
focusto('system_capacity');
geom = widgetpos('system_capacity');
transp( geom, 'yellow', 70 );
millisleep(500);

sysSize = in( "What is your desired PV array size (kW)?", 
			   get('system_capacity'), 
			   "Desired PV Array Size",
			   [defaultX, defaultY] );
exitIf(sysSize == "");

sysSize = to_real(sysSize);//sdt

set('system_capacity', sysSize );

// Review time
millisleep(1000);
transp( geom, 'yellow', 0 );





/*************************************/
/**            Set Costs           **/
/* could possibly do conversions.... skip fo now
// Assumptions 1 INR = 0.015 USD conversion rate 1/30/2017
convINRtoUSD = 0.015;
convINRtoUSD = in( "SAM calculates all values in USD. This Wizard allows entries in INR assuming a INR to USD conversion rate. Enter the desired conversion rate or hit enter.", 
                  convINRtoUSD, 
                  'Rupess to Dollars',
                  [defaultX, defaultY]);
exitIf( convINRtoUSD  <= 0 );
*/
// Get capital cost from user - all costs in BOS bucket per file from Sara
// capCostINR = to_real(get('bos_equip_fixed')) / convINRtoUSD;

show_page('System Costs');

// Highlight resource selection box in UI
focusto('per_module');
geom = widgetpos('per_module');
transp( geom, 'yellow', 70 );
millisleep(500);

//sdt -added rupee in text
capitalCost = in( "What is your system's capital cost per Watt in rupees?", 
                  get('per_module') , 
                  'Capital Cost',
                  [defaultX, defaultY]);
exitIf( capitalCost  == "" );

// Ensure user input is greater than zero
while( to_real(capitalCost) < 0 ){
	capitalCost = in( "Capital cost must be a value greater than zero", 
						  get('per_module'), 
						  "Capital Cost",
		                  [defaultX, defaultY]);
	exitIf( capitalCost  == "" );
}
capitalCost= to_real(capitalCost);//sdt

set('per_module', capitalCost );
transp();

/* Fixed Annual Maintenance Costs */
// Highlight resource selection box in UI
focusto('om_capacity');
geom = widgetpos('om_capacity');
transp( geom, 'yellow', 70 );
millisleep(500);


omCostAry = get('om_capacity');
omCost = omCostAry[0];
//sdt - added O&M rupees to O&M cost description
omCost = in( "What is your annual maintenance cost in Rupees/kW?", 
                  omCost, 
                  'Annual Maintenance',
                  [defaultX, defaultY]);
exitIf( omCost  == "" );

// Ensure user input is greater than zero
while( to_real(omCost) < 0 ){
	omCost = in( "Maintenance cost must be a value greater than zero", 
						  omCost, 
						 'Annual Maintenance',
		                  [defaultX, defaultY]);
	exitIf( omCost  == "" );
}
omCostAry[0] = to_real(omCost);

set('om_capacity', omCostAry );
transp();

/* Escalation without inflation */
// Highlight resource selection box in UI
focusto('om_capacity_escal');
geom = widgetpos('om_capacity_escal');
transp( geom, 'yellow', 70 );
millisleep(500);
inflation = get('inflation_rate');
nominalEsc = get('om_capacity_escal');
omCostEsc =  to_real(nominalEsc) + to_real(inflation);

omCostEsc = in( "What is your maintenance acceleration rate?", 
                   omCostEsc, 
                  'Maintenance acceleration',
                  [defaultX, defaultY]);
exitIf( omCostEsc  == "" );


nominalEsc = to_real(omCostEsc) - to_real(inflation);
set('om_capacity_escal', nominalEsc); 

// Review time
// Can add message about escalation above inflation
millisleep(1000);
transp();


/*************************************/
/**            Set Finances         **/
show_page('Financial Parameters');

// Highlight resource selection box in UI
focusto('debt_percent');
geom = widgetpos('debt_percent');
transp( geom, 'yellow', 70 );
millisleep(500);


debtPercent = in( "What is your debt percent?", 
                  get('debt_percent') , 
                  'Debt Percent',
                  [defaultX, defaultY]);
exitIf( debtPercent  == "" );

// Ensure user input is greater than zero
while( (to_real(debtPercent) < 0) ||  (to_real(debtPercent) > 100)){
	debtPercent = in( "Debt percent must be a value between 0 and 100", 
						  get('debt_percent'), 
						  "Debt Percent",
		                  [defaultX, defaultY]);
	exitIf( debtPercent  == "" );
}
debtPercent = to_real(debtPercent);

set('debt_percent', debtPercent );
transp();


focusto('term_int_rate');
geom = widgetpos('term_int_rate');
transp( geom, 'yellow', 70 );
millisleep(500);


interestRate = in( "What is your interest rate?", 
                  get('term_int_rate') , 
                  'Interest Rate',
                  [defaultX, defaultY]);
exitIf( interestRate  == "" );

// Ensure user input is greater than zero
while( (to_real(interestRate) < 0) ||  (to_real(interestRate) > 100) ){
	interestRate = in( "Interest rate must be a value between 0 and 100", 
						  get('term_int_rate'), 
						  "Interest Rate",
		                  [defaultX, defaultY]);
	exitIf( interestRate  == "" );
}

interestRate = to_real(interestRate);
set('term_int_rate', interestRate );
transp();


focusto('loan_moratorium');
geom = widgetpos('loan_moratorium');
transp( geom, 'yellow', 70 );
millisleep(500);


loanMoratorium = in( "What is your loan moratorium?", 
                  get('loan_moratorium') , 
                  'Loan Moratorium',
                  [defaultX, defaultY]);
exitIf( loanMoratorium  == "" );

// Ensure user input is greater than zero
while( to_real(loanMoratorium) < 0 ){
	loanMoratorium = in( "Loan moratorium must be a value greater than zero", 
						  get('loan_moratorium'), 
						  "Loan Moratorium",
		                  [defaultX, defaultY]);
	exitIf( loanMoratorium  == "" );
}

loanMoratorium = to_real(loanMoratorium);
set('loan_moratorium', loanMoratorium );



// inflation rate
focusto('inflation_rate');
geom = widgetpos('inflation_rate');
transp( geom, 'yellow', 70 );
millisleep(500);


inflationRate = in( "What is your inflation rate?", 
                  get('inflation_rate') , 
                  'Inflation rate',
                  [defaultX, defaultY]);

exitIf( inflationRate  == "" );

// Ensure user input is greater than zero
while( to_real(inflationRate) < 0 ){
	inflationRate = in( "Inflation rate must be a value greater than zero", 
						  get('inflation_rate'), 
						  "Inflation rate",
		                  [defaultX, defaultY]);
	exitIf( inflationRate  == "" );
}

inflationRate = to_real(inflationRate);
set('inflation_rate', inflationRate );



// federal income tax rate - array
focusto('federal_tax_rate');
geom = widgetpos('federal_tax_rate');
transp( geom, 'yellow', 70 );
millisleep(500);


taxRateAry = get('federal_tax_rate');
taxRate = taxRateAry[0];


taxRate = in( "What is your tax rate?", 
                  taxRate, 
                  'Tax rate',
                  [defaultX, defaultY]);
exitIf( taxRate  == "" );

// Ensure user input is greater than zero
while( to_real(taxRate) < 0 ){
	taxRate = in( "Tax rate must be a value greater than zero", 
						  taxRate, 
						  "Tax rate",
		                  [defaultX, defaultY]);
	exitIf( taxRate  == "" );
}

taxRateAry[0] = taxRate;

set('federal_tax_rate', taxRateAry );

// o&m reserve -sdt - added O&M reserve input
focusto('months_working_reserve');
geom = widgetpos('months_working_reserve');
transp( geom, 'yellow', 70 );
millisleep(500);


workingReserve = in( "What is the number of months of working capital (O&M) reserve?", 
                  get('months_working_reserve') , 
                  'Working capital reserve months',
                  [defaultX, defaultY]);
exitIf( workingReserve  == "" );

// Ensure user input is greater than zero
while( to_real(workingReserve) < 0 ){
	workingReserve = in( "Number of months must be a value greater than zero", 
						  get('months_working_reserve'), 
						  "Working capital reserve months",
		                  [defaultX, defaultY]);
	exitIf( workingReserve  == "" );
}

workingReserve = to_real(workingReserve);

set('months_working_reserve', workingReserve );


// receivables reserve
focusto('months_receivables_reserve');
geom = widgetpos('months_receivables_reserve');
transp( geom, 'yellow', 70 );
millisleep(500);


receivablesReserve = in( "What is the number of months of receivables reserve?", 
                  get('months_receivables_reserve') , 
                  'Receivables reserve months',
                  [defaultX, defaultY]);
exitIf( receivablesReserve  == "" );

// Ensure user input is greater than zero
while( to_real(receivablesReserve) < 0 ){
	receivablesReserve = in( "Number of months must be a value greater than zero", 
						  get('months_receivables_reserve'), 
						  "Receivables reserve months",
		                  [defaultX, defaultY]);
	exitIf( receivablesReserve  == "" );
}

receivablesReserve= to_real(receivablesReserve);
set('months_receivables_reserve', receivablesReserve );


// interest on reserves
focusto('reserves_interest');
geom = widgetpos('reserves_interest');
transp( geom, 'yellow', 70 );
millisleep(500);


reserveRate = in( "What is your reserves interest rate?", 
                  get('reserves_interest') , 
                  'Reserves interest rate',
                  [defaultX, defaultY]);
exitIf( reserveRate  == "" );

// Ensure user input is greater than zero
while( to_real(reserveRate) < 0 ){
	reserveRate = in( "Reserves interest rate must be a value greater than zero", 
						  get('reserves_interest'), 
						  "Reserves interest rate",
		                  [defaultX, defaultY]);
	exitIf( reserveRate  == "" );
}
reserveRate = to_real(reserveRate);
set('reserves_interest', reserveRate );

// Possibly ROE array entry in the future

// Possibly ROE array entry in the future


// ROE - array
focusto('roe_input');
geom = widgetpos('roe_input');
transp( geom, 'yellow', 70 );
millisleep(500);


roeAry = get('roe_input');
if (#roeAry < 1)
	roe10 = 20;
else
	roe10 = roeAry[0];
if (#roeAry < 10)
	roeon = 24;
else
	roeon = roeAry[10];

if (#roeAry < 50)
	roeAry = alloc(50);


roe10 = in( "ROE for the first 10 years?", 
                  roe10, 
                  'ROE',
                  [defaultX, defaultY]);
exitIf( roe10  == "" );

// Ensure user input is greater than zero
while( to_real(roe10) < 0 ){
	roe10 = in( "ROE must be a value greater than zero", 
						  roe10, 
						  "ROE",
		                  [defaultX, defaultY]);
	exitIf( roe10  == "" );
}

roeon = in( "ROE for the 11th year on?", 
                  roeon, 
                  'ROE',
                  [defaultX, defaultY]);
exitIf( roeon  == "" );

// Ensure user input is greater than zero
while( to_real(roeon) < 0 ){
	roeon = in( "ROE must be a value greater than zero", 
						  roeon, 
						  "ROE",
		                  [defaultX, defaultY]);
	exitIf( roeon  == "" );
}

for (i=0;i<#roeAry;i++)
{
	if (i<10)
		roeAry[i] = roe10;
	else
		roeAry[i] = roeon;
}

set('roe_input', roeAry );

// TODO - possible add depreciation input

// Review time
// Can add message about escalation above inflation
millisleep(1000);
transp();




/*************************************/
/**          Finish Wizard          **/

// Ask user if they want to run the simulation
//  if not, exit the wizard
runSim = yesno("All inputs complete. Do you want to run the simulation now?\n\n(selecting 'no' will exit the wizard and enter the regular SAM application)",
				[defaultX, defaultY] );


	
if( runSim ){
	simMessages = "";
	if( !simulate(simMessages, true) ){
		msgbox( simMessages );
		exit;
	}
	show_page('Results');

/* Show HTML results summary */	
/* TODO - check to lcoe with ROE */

//outvars = ['lcoe_nom', 'project_return_aftertax_npv','flip_actual_irr', 'flip_actual_year', 'min_dscr'];
case = case_name();
str_html = '<html><body>' + 
           '<h3><font size = "4">Results Summary</font></h3>' + 
           '<table bgcolor=#dddddd>' + 
           '<tr><th>Metric</th><th>Value</th></tr>';
/*	for( i=0; i<#outvars; i++) 
	{
          str_html += '<tr  bgcolor=#ffffff><td align="left">' +  varinfo( outvars[i] ){'label'} + '</td><td align="left">' + get(outvars[i]) + ' ' + varinfo( outvars[i] ){'units'} + '</td></tr>';
    }
*/    
//sdt - adjusted output formats to denote rupees       
	str_html += '<tr  bgcolor=#ffffff><td align="left">Levelized COG (nominal)</td><td align="left">' + sprintf('%.2f',to_real(get('lcog'))/100) + ' Rs/kWh</td></tr>';  // _
	str_html += '<tr  bgcolor=#ffffff><td align="left">Net present value</td><td align="left">' + 'Rs ' + sprintf('%,',round(to_real(get('project_return_aftertax_npv')))) + '</td></tr>';
	str_html += '<tr  bgcolor=#ffffff><td align="left">Internal rate of return (IRR)</td><td align="left">' + sprintf('%.2f',to_real(get('flip_actual_irr'))) + ' %</td></tr>';
	str_html += '<tr  bgcolor=#ffffff><td align="left">Year IRR is achieved</td><td align="left">' + sprintf('%.0f',to_real(get('flip_actual_year'))) + '</td></tr>';
	str_html += '<tr  bgcolor=#ffffff><td align="left">Minimum debt service coverage ratio (DSCR)</td><td align="left">' + sprintf('%.2f',to_real(get('min_dscr'))) + '</td></tr>';
	str_html +=  '</table><br><br><br>';

// cash flow line items

	varcf = ['cf_om_capacity_expense','cf_feddepr_total','cf_debt_payment_interest','cf_reserve_interest','cf_return_on_equity_dollars','cf_lcog_costs'];
    str_html += '<table bgcolor=#dddddd width=100%>';
    ap = round(to_real(get('analysis_period')));

    str_html += '<tr><th>year</th>';
    for (i=0;i<=ap;i++)
    {
		str_html +=  '<th>' + sprintf("%d",i) + '</th>';
	}
    str_html += '</tr>';

    for (i=0;i<#varcf;i++)
    {
		str_html += '<tr  bgcolor=#ffffff><td align="left">' + varinfo(varcf[i]){'label'} + ' ' + varinfo(varcf[i]){'units'} + '</td>';
		cfline = get(varcf[i]);
		for (j=0; j<#cfline; j++)
		{
			str_html += '<td align="right">' + 'Rs ' + sprintf('%,',round(cfline[j])) + '</td>';
		}
		str_html += '</tr>';
    }
	str_html +=  '</table></body></html>';


html_dialog ( str_html , 'System Results for "' + case + '" Case' , [300,300,2600,600]);

	
} else {
	exit;
}



/*************************************/
/**          Tornado Chart          **/
plotLocation="";
// Ask user if they would like to make a tornado chart
//  if not, skip to report generation
// add lcoe_roe output
runTornado = yesno("Would you like to run a few parametrics and generate a tornado chart?",
					[defaultX, defaultY]);
if (runTornado){
	variation = 0.10;
	outLabel = 'lcog';
	vars = [ 'system_capacity', 'per_module', 'om_capacity',
			'debt_percent',	'term_int_rate','loan_moratorium'];
	
	/* TODO - make function or retrieve additional data in varinfo */
	isarray = [ 0, 0, 1, 0, 0, 0];
	
	baseVals = alloc( #vars );
	for( i=0; i<#vars; i++) 
	{
		baseVals[i] = get( vars[i] );
	}
	
	baseOutput = get(outLabel);
	outputData_up = alloc( #vars, 2 );
	outputData_dn = alloc( #vars, 2 );
	
	maxVal = 0;
	minVal = 1000;
	avgChanges = alloc( #vars );
	for( i=0; i<#vars; i++) avgChanges[i]=0;
	
	for( i=0;i<#vars;i++){
	
		simMessages = "";
		
		// Run a var + variation sim
		if (isarray[i] == 1)
			set( vars[i], [baseVals[i][0] * (1+variation)] );
		else
			set( vars[i], baseVals[i] * (1+variation) );
		if(!simulate(simMessages, false)){
			outln("Sim Error\n" + simMessages +"\n\nExiting");
			exit;
		}
		
		tmp = get(outLabel);
		if(tmp>maxVal) maxVal = tmp;
		if(tmp<minVal) minVal = tmp;
		
		outputData_up[i] = tmp;
		avgChanges[i] += abs(baseOutput - tmp)/2/baseOutput;

		// Run a var - variation sim
		if (isarray[i] == 1)
			set( vars[i], [baseVals[i][0] * (1-variation)] );
		else
			set( vars[i], baseVals[i] * (1-variation) );
		if(!simulate(simMessages, false)){
			outln("Sim Error\n" + simMessages);
			exit;
		}
		tmp = get(outLabel);
		if(tmp>maxVal) maxVal = tmp;
		if(tmp<minVal) minVal = tmp;
		
		outputData_dn[i] = tmp;
		avgChanges[i] += abs(baseOutput - tmp)/2/baseOutput;
		
		// Reset all parameters
		for( k=0; k<#vars; k++) 
		{
			set( vars[k], baseVals[k] );
		}
	}
	
	range = maxVal - minVal;
	// Sort according to average change
	sortedChanges = avgChanges;
	stable_sort( sortedChanges );
	order = alloc(#vars);
	usedFlag = alloc(#vars);
	
	for(i=0; i<#vars; i++){
		order[i] = 0;
		usedFlag[i] = false;
	}
	
	for(i=0; i<#vars; i++){
	for(j=0; j<#vars; j++){
		if( avgChanges[j] == sortedChanges[i] && usedFlag[j]==false ){
			order[i] = j;
			usedFlag[j] = true;
			break;
		}
	}}

	//update plot data	
	yvals_dn = alloc( #vars );
	yvals_up = alloc( #vars );
	xvals_dn = alloc( #vars );
	xvals_up = alloc( #vars );
	labels = alloc( #vars,2 );

	for(i=0; i<#vars; i++){
		yvals_dn[i] = 0.5 + 2*i;
		yvals_up[i] = 1.0 + 2*i;
		xvals_dn[i] = outputData_dn[order[i]];
		xvals_up[i] = outputData_up[order[i]];

		labels[i][0] = 0.75 + 2*i;
		labels[i][1] = varinfo( vars[ order[i] ] ){'label'};
	}
	
	// create the plot
	newplot();
	plot( xvals_dn, yvals_dn, {'type'='hbar', 'baseline'=baseOutput, 'color'=[140,140,140], 'series'='(--)', thick=20});
	plot( xvals_up, yvals_up, {'type'='hbar', 'baseline'=baseOutput, 'color'=[240,100,100], 'series'='(++)', thick=20});

	axis('x1', {'min'=minVal-range*0.05, 'max'=maxVal+range*0.05, 'label'= varinfo(outLabel){'label'} });
	axis('y1', { 'type'='label', 'labels'=labels, 'min'=0, 'max'=2*#vars} );
	plotopt({'legendpos'='bottom', 'coarse'=true, 'fine'=true, 'title'='LCOG Sensitivity (+/- 10%)'});
	annotate( [baseOutput, 0], [baseOutput, 2*#vars], {type='line', color='black'});

	
	plotLocation = tempfile('png');
	plotout(plotLocation);
}

/***************************************/
/** LCOG plot **/
runSector = yesno("Would you like to create a LCOG sector plot?",
					[defaultX, defaultY]);
if (runSector){
	variation = 0.10;
	outLabel = 'lcog';
	vars = [ 'lcog_om', 'lcog_depr', 'lcog_loan_int',
			'lcog_wc_int',	'lcog_roe'];
	
	sector_labels = [ 'O and M', 'Depreciation', 'Loan interest',
			'Reserve interest',	'ROE'];

	//update plot data	
	labels = alloc( #vars,2 );

	for(i=0; i<#vars; i++){
		labels[i][0] = get(vars[i]);
		labels[i][1] = sector_labels[i];
//		labels[i][1] = varinfo( vars[i] ){'label'};
	}
	stitle = "Levelized cost of generation " + get("lcog")/100 + "Rs/kWh"; //sdt
	// create the plot
	newplot();
	sector( labels, {border=0, holesize=0} );
	plotopt( { title=stitle, showaxes=false, border=0, pdffontface='ComputerModernSerif'});
	
	sectorLocation = tempfile('png');
	plotout(sectorLocation);
}




/***************************:w**********/
/**        Report Generation        **/

// Ask user if they would like to create a report based off this simulation
//  if not, exit wizard

if( yesno("Would you like to generate a report based off these results?",[defaultX, defaultY]) ){
	
	if (runTornado && runSector){
		newplot("");
		data = {'plotTornado' = 1,
				'plotLocation' = plotLocation,
				'plotSector' = 1,
				'sectorLocation' = sectorLocation};
	}
	elseif( runTornado ){
		newplot("");
		data = {'plotTornado' = 1,
				'plotLocation' = plotLocation};
	} 
	elseif( runSector ){
		newplot("");
		data = {'plotSector' = 1,
				'sectorLocation' = sectorLocation};
	} else {
		data = {'plotTornado' = 0,
				'plotLocation' = '',
				'plotSector' = 0,
				'sectorLocation' = ''};
	}
	
	// Create report
	if( yesno("This report will be saved to:\n" + homedir()+"/PVforIndiaReport.pdf\n\nSelect 'Yes' to continue or 'No' to enter a different file path",[defaultX, defaultY]) ){
		reportFile = homedir()+"/PVforIndiaReport.pdf";
	} else {
		reportFile = choose_file( homedir(), "Output report file", 'PDF Files (*.pdf)|*.pdf');
	}
	
	ok = pdfreport( reportFile, 
					{'template'=runtimedir() + 'quickstart/pvforindiawizard.samreport', 'metadata'=data });
	
	// Open Report
	browse(reportFile);
} 

if (left(ostype(),3) == "win")
{
	if( yesno("Would you like to look at an Excel workbook based off these results?",[defaultX, defaultY]) ){

		source = runtimedir() + 'spreadsheets/PV_for_India.xlsx';
		destination = userlocaldatadir() + '/PV_for_India.xlsx';

		destination = copy_file( source, destination );
		
		xl = xl_create( destination );
		capacity_mw = to_real(get('modulearray_power')) / 1000; 
		xl_set( xl, capacity_mw, 'capacity_mw');
		cap_fac= to_real(get('capacity_factor'))/100;
		xl_set( xl, cap_fac, 'capacity_factor');
		xl_set( xl, get('analysis_period'), 'analysis_period');
		xl_set( xl, get('annual_energy'), 'generation'); //sdt - added output excel for annual generation


		cost_per_mw =  to_real(get('installed_per_capacity')) * 10; // Rs Lakh/MW
		xl_set( xl, cost_per_mw, 'cost_per_mw');
		
		xl_set( xl, get('debt_percent'), 'debt_percent');
		xl_set( xl, get('loan_moratorium'), 'loan_moratorium');
		xl_set( xl, get('term_tenor'), 'term_tenor');
		xl_set( xl, get('term_int_rate'), 'term_int_rate');
		
		equity = to_real(get('issuance_of_equity'))/100000; // RsLac
		reserve0 = get('cf_reserve_receivables');// RsLac
		xl_set( xl, equity, 'equity');
		xl_set( xl, equity - reserve0[0]/100000, 'equity_wo_receivables');
		
		roeAry = get('roe_input');
		xl_set( xl, roeAry[0], 'roe_10');
		xl_set( xl, roeAry[10], 'roe_11');
		
		xl_set( xl, to_real(get('nominal_discount_rate'))*100, 'nominal_discount_rate');
		xl_set( xl, get('federal_tax_rate'), 'tax_rate');
		
		deprAry = get('depr_custom_schedule');
		xl_set( xl, deprAry[0], 'depr_12');
		xl_set( xl, deprAry[12], 'depr_13');
		
		xl_set( xl, get('months_receivables_reserve'), 'months_receivables_reserve');
		xl_set( xl, get('reserves_interest'), 'reserves_interest');
		
		om_mw = get('om_capacity'); //Rs Lakh/MW
		xl_set( xl, om_mw[0]/100, 'om_mw');
		inflation_rate=get('inflation_rate');
		esc_rate=get('om_capacity_escal');
		xl_set( xl, to_real(inflation_rate) + to_real(esc_rate), 'om_acceleration');
		
		xl_set( xl, get('ppa_price_input'), 'ppa_price');


		xl_set( xl, to_real(get('lcog'))/100,'lcog_summary');
		xl_set( xl, to_real(get('project_return_aftertax_npv')),'npv_summary');
		xl_set( xl, to_real(get('flip_actual_irr')),'irr_summary');
		xl_set( xl, to_real(get('flip_actual_year')),'irr_year_summary');
		xl_set( xl, to_real(get('min_dscr')),'dscr_summary');
			
	}	
}

// Delete temporary tornado plot
//if (file_exists(plotLocation))
//	remove_file( plotLocation );

run_wizard_again = yesno("Would you like to rerun the wizard?",
					[defaultX, defaultY]);

} // end running wizard
