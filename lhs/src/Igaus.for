C     Last change:  S    26 Jan 98   10:47 am
C****************************************************************
C SUBROUTINE IGAUS GENERATES INVERSE GAUSSIAN DISTRIBUTIONS WITH
C PARAMETERS MU AND LAMBDA.  AN ACCEPTANCE-REJECTION SCHEME DUE TO
C ATKINSON IS USED TO GENERATE 10,000 RANDOM OBSERVATIONS FROM
C AN INVERSE GAUSSIAN DISTRIBUTION WITH PARAMETERS MU AND LAMBDA
C AND THE RESULTING EDF IS SAMPLED TO GENERATE THE DESIRED DISTRIBUTION.
C THE MEAN OF THIS DISTRIBUTION IS MU AND THE VARIANCE IS MU**3/LAMBDA.
C
C REFERENCE: A. C. ATKINSON (1982) "THE SIMULATION OF GENERALIZED INVERSE
C            GAUSSIAN AND HYPERBOLIC RANDOM VARIABLES,"
C            SIAM J. SCI. STAT. COMPUT.
C            VOL. 3, NO. 4, DEC. 1982, 502-515.
C
      SUBROUTINE IGAUS(J)
C     INCLUDE 'KILLFILE.INC'                                            GDW-96  
      USE KILLFILE                      
C
C     INCLUDE 'PARMS.INC'                                               GDW-96  
      USE PARMS                         
C     INCLUDE 'CPARAM.INC'                                              GDW-96  
      USE CPARAM                        
C     INCLUDE 'CSAMP.INC'                                               GDW-96  
      USE CSAMP                         
C     INCLUDE 'CWORKX.INC'                                              GDW-96  
      USE CWORKX                        
C
      REAL MU,LAMBDA,IGAUSF
C
      LOC(I,J)=(J-1)*N+I
C
      PROBINC=1./FLOAT(N)
      IF (IRS .EQ .1) PROBINC=1.0
      STRTPT=0.
      READ (8) MU,LAMBDA
C
C     CONVERT MU AND LAMBDA TO THE CHI AND PSI NOTATION USED BY
C     ATKINSON AS FOLLOWS:
C
      CHI = LAMBDA
      PSI = LAMBDA/MU**2
C
C     TO GENERATE THE INVERSE GAUSSIAN WITH ATKINSON'S ALGORITHM IT IS
C     NECESSARY TO USE FL = -.5.  HOWEVER, FOR VALUES OF FL < 0 THE
C     FOLLOWING RELATIONSHIP SHOULD BE USED:
C
C          IF     X ~ INVERSE GAUSS(FL,PSI,CHI)
C          THEN 1/X ~ INVERSE GAUSS(-FL,CHI,PSI)
C
C     THAT IS, INTERCHANGE CHI AND PSI IN THE CALLING STATEMENT AND
C     TAKE 1/X AS THE GENERATED VALUE RATHER THAN X.
C     SET THE CONSTANTS REQUIRED IN FUNCTION GIGLT1
C
      FL = .5
      CALL IGAUS1(FL,PSI,CHI)
      If(KLLERR) Return
C
C     GENERATE "NSIM*10" RANDOM DEVIATES FROM THE INVERSE GAUSSIAN
C     DISTRIBUTION WITH PARAMETERS MU AND LAMBDA
C
      NOBS = 2*MAXTB
      DO 3 I = 1,NOBS
         XX(I) = 1./IGAUSF()
    3 CONTINUE
C
C     SORT THE RANDOM DEVIATES TO FORM THE EDF
C
      CALL SIFT(XX,NOBS)
      If(KLLERR) Return
C
C     GENERATE THE DESIRED SAMPLE BY SAMPLING FROM THE EDF
C
      DO I = 1, N
         R = PROBINC * RNUM1() + STRTPT
         NR = MAX( 1, NINT(R*NOBS))
         RES = XX(NR)
         X(LOC(I,J)) = MAX(RES,1.0E-10)
         IF (IRS == 0) STRTPT = REAL(I) / REAL(N)
      END DO
C
      RETURN
      END
