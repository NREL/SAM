
function read_SAM( name )
{
	file = '../' + name +'_SAM.csv';
	data = csvread( file, {skip=4,table=true,numeric=true});
	sf = data{'0.0 %'};	
	outln( file + ': ' + @data + ': ' + #sf + ' values.' );
	return sf;
}

function read_PVSOL( name )
{
	file = '../' + name + '_PVSOL.csv';
	data = csvread( file, {skip=0,table=true,numeric=true});
	sf = data{'Loss Percent [%]'};	
	outln( file + ': ' + @data + ': ' + #sf + ' values.' );
	return sf;
}

function read_PVsyst( name )
{
	file = '../' + name +'_PVsyst.csv';
	data = csvread( file, {skip=13,numeric=true,delim=';'});
	
	sf = alloc(8760);
	for( i=0;i<8760;i++ )
	{
		sf[i] = 100-100*data[i][8];
		if ( data[i][2] == 0 )
			sf[i] = 100;
	}
		
	outln( file + ': ' + #data + ': ' + #sf + ' values.' );
	return sf;
}


function extract( array, start, length )
{
	x = alloc(length);
	for( i=0;i<length;i++ )
		x[i] = array[start+i];
	return x;
}


jan1_idx = 0;
apr1_idx = 2160;
jul1_idx = 4344;
oct1_idx = 6552;

day_names = [ 'January 1st', 'April 1st', 'July 1st', 'October 1st' ];
days = [ jan1_idx, apr1_idx, jul1_idx, oct1_idx ];

day_start = 0;
day_hours = 24;

for( i=0;i<day_hours;i++ )
	time[i] = i+day_start+0.5;
	

models = [ 'SAM', 'PVsyst', 'PV*SOL' ];
loadfuncs = [ read_SAM, read_PVsyst, read_PVSOL ];
colors = [ 'black', 'light grey', 'grey' ];
styles = [ 'solid', 'dot', 'dash' ];
sizes = [ 2, 1, 2 ];
markers = [ 'none', 'circle', 'none' ];


function make_plots( names, labels )
{
	nmodels = #loadfuncs;
	for( itest=0;itest<#names;itest++ )
	{
		for (i=0;i<nmodels;i++ )
		{
			data[i] = loadfuncs[i]( names[itest] );
			outln( #data[i] );
		}
		
		newplot(true);

		for( i=0;i<#days;i++ )
		{
			newplot();
			for( m=0;m<nmodels;m++ )
			{
				sf = extract( data[m], days[i]+day_start, day_hours );
				plot( time, sf,
					{ series=models[m], style=styles[m], marker=markers[m], 
						size=sizes[m], color=colors[m] } );
			}
			plotopt( {title= labels[itest] + ' on ' + day_names[i] } );
			axis( 'x1', {label='Time of day (hour)'});
			axis( 'y1', {label='Linear shade loss (%)'});
			file = lower(labels[itest] + '_' + (i+1) + '.pdf' );
			file = replace(file, ' ', '_' );
			plotout( file );
			
		}
	}
}

make_plots( [ 'test1', 'test2', 'test3' ], ['Denver', 'Quito', 'Perth' ] );


models = [ 'SAM', 'PV*SOL' ];
loadfuncs = [ read_SAM, read_PVsyst, read_PVSOL ];
make_plots( [ 'ivanhoe', 'babbitt', 'halsted', 'paradise' ],
 [ 'Ivanhoe in Denver', 'Babbitt in Los Angeles', 'Halsted in Los Angeles', 'Paradise in Soquel' ] );

