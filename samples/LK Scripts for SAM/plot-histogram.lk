////////////////////////////////////////////////////////////////////////////////
/*
	This script demonstrated the use of the plot() function to create a
	histogram using data in the plot-histogram-data.csv file. 
	
	To run the script, put the plot-histogram.lk and plot-histogram-data.csv
	files in the same folder.
	
	Tested in SAM 2020.2.29 r2
*/
////////////////////////////////////////////////////////////////////////////////

file = cwd() + '/plot-histogram-data.csv';
data = csvread( file, {'numeric'=true, 'table'=true});
outln(data);
cols = @data;
nbins = 10;

colors = ['red', 'blue', 'black', 'green'];
newplot();
ymax = 0;

for( i=0;i<#cols;i++ )
{
	x = data{cols[i]};
	n = #x;
	xmin = min(x);
	xmax = max(x);
	
	if ( i==0 )
	{
		axmin = xmin;
		axmax = xmax;
	}
	else
	{
		if ( xmin < axmin ) axmin = xmin;
		if ( xmax > axmax ) axmax = xmax;
	}
	
	stable_sort(x);
	
	binwidth = (xmax-xmin)/nbins;
	for( j=0;j<nbins;j++ )
	{
		binx[j] = xmin + j*binwidth + 0.5*binwidth;
		biny[j] = 0;
	}
	
	ibin = 0;
	for( j=0;j<n;j++ )
	{
		xj = x[j];
		binmax = xmin+(ibin+1)*binwidth;
		if ( ibin >= nbins )
			outln('error');
			
		if ( xj <= binmax )
		{
			biny[ibin]++;
			if ( biny[ibin]>ymax) ymax=biny[ibin];
		}
		else		
			ibin++;
	}
		
	plot( binx, biny, {'type'='bar', 'thick'=1, 'color'=colors[mod(i,#colors)], 'series'=cols[i]});	
}

axis('x1', { 'min'=axmin-0.05*(axmax-axmin), 'max' = axmax+0.05*(axmax-axmin), 'label'='LCOE (\\cent /kWh)' });
axis('y1', {'min'=0, 'max'=ymax*1.05, 'label'='Frequency'});
plotopt({'legendpos'='right', 'coarse'=false, 'fine'=false, 'title'='Stochastic LCOE distributions'});