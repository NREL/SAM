// Read a weather file
inFile = choose_file(homedir(), "Choose an hourly weather file", "SAM CSV files (*.csv)|*.csv", 0);

fIn = open(inFile, 'r');

header = ["",""];
read_line( fIn, header[0] );
read_line( fIn, header[1] );

close( fIn);

wf = csvread(inFile, { "delim"=',', "skip"=2, "table"=true });
oldLabels = @wf;

// Generate an empty wf with the desired size
outln("Converting file...");

SIZE = 8760 * 4;   // 15 minute data
newLabels = ['Year','Month','Day','Hour','Minute','GHI','DNI','DHI','Tdry','Tdew','RH','Pres','Wspd','Wdir','Snow Depth'];
newWF = {};

for( i=0; i<#newLabels; i++){
	newWF{ newLabels[i] } = alloc(SIZE);
}

// Fill the empy wf
for(i=0; i<8760; i++){
for(j=0; j<4; j++){
	
	wi = i*4 + j;
	
	minute = j*15;
	newWF{"Minute"}[wi] = minute;
	
	for( k=0; k<#oldLabels; k++){
		newWF{ oldLabels[k] }[wi] = wf{oldLabels[k]}[i];
	}
	
}}

// Write a new wF
outln("Writing file...");

outputName = path_only(inFile) + "/" + file_only(inFile) + "_15min.csv";

fOut = open(outputName, 'w');
write_line(fOut, header[0]);
write_line(fOut, header[1]);

csvwrite( "tmp.csv", newWF);

fTmp = open("tmp.csv", 'rb');

for( i=0; i<8760*4 + 1; i++){
	line = "";
	read_line( fTmp, line);
	write_line( fOut, line);
	}

close(fTmp);
remove_file( "tmp.csv");
close(fOut);

outln("15-minute data saved to " + outputName + ".");