/*

This script demonstrates options for setting the weather file for SAM's 
solar performance models.

It does the following three things:

1. Show the current weather file, and display values of library metadata that
   appears on the Location and Resource page.
   
2. Set the weather file to one from SAM's solar resource library.

3. Set the weather file to a file stored on disk that is not in the solar
   resource library.
   
The variable use_specific_weather_file determines whether SAM uses a weather
file from the library or on disk. In SAM's user interface, it appears as a check 
box on the Location and Resource page under "Use a specific weather file on 
disk" at the bottom of the page.

The variable solar_resource is the current selection in the solar resource
library. When use_specific_weather_file = 0, SAM uses the library selection as
the weather file for the simulation.

When use_specific_weather_file = 1, SAM uses the file specified by  
user_specified_weather file as the weather file and ignores the current library
selection.

user_specified_weather is a string for the full path of the weather file, for 
example: user_specified_weather_file = "c:/My Weather Data/weather file.csv"

This script also demonstrates the use of the choose_file() function.

*/

outln("Running script for Case: " + case_name() + "\n");

outln("============================");
outln("1. Show current weather file");
outln("============================\n");

file= get('solar_resource_file');
outln("The current solar resource file is:");
outln(file);

source=get('solar_data_source');
name=get('solar_data_file_name');
location=get('solar_resource');
outln("\nMetadata for the selected location in the solar resource library (may not match current weather data):");
outln("Data source="+source);
outln("Data file="+name);
outln("Name for library list="+location);

outln("\n============================");
outln("2: Choose file from solar resource library");
outln("============================\n");

set('use_specific_weather_file',0);
library_choices = library("SolarResourceData");
library_selection = choose_from_list(library_choices,"Choose a location from the library:","Choose from Library",0);
set("solar_resource",library_selection);

file= get('solar_resource_file');
outln("The solar resource file is now (from library):");
outln(file);

source=get('solar_data_source');
name=get('solar_data_file_name');
location=get('solar_resource');
outln("\nMetadata for the selected location in the solar resource library (should match current file):");
outln("Data source="+source);
outln("Data file="+name);
outln("Name for library list="+location);

outln("\n============================");
outln("2: Choose a file stored on disk");
outln("============================\n");

set('use_specific_weather_file',1);

new_file = choose_file( homedir(),"Choose a New Weather File:","Solar Resource Files (*.csv, *.epw, *.tm2)|*.csv; *.epw; *.tm2",0,0 );
set('user_specified_weather_file', new_file);

file= get('solar_resource_file');
outln("The solar resource file is now (from disk):");
outln(file);

source=get('solar_data_source');
name=get('solar_data_file_name');
location=get('solar_resource');
outln("\nMetadata for the selected location in the solar resource library (may not match current file):");
outln("Data source="+source);
outln("Data file="+name);
outln("Name for library list="+location);